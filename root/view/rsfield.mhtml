<%doc>
This component renders the value one field of an object.  If the field contains
the id of another object it will be rendered as a link to the
referenced object.  If the field is the primary id key of the current object,
it will be rendered as a 'details' link.
</%doc>

<%args>
$object
$col
</%args>

<span class="field_value">
% if ($field_type eq 'table_id') {
  <span class="this_id_field">
    <a href='<% $c->uri_for("/view/object", $type, SmallRNA::DB::id_of_object($object)) %>'>[details&nbsp;...]</a>
  </span>
% } else {
%   if ($field_type eq 'foreign_key' && $field_value && defined $ref_display_key) {
  <span class="ref_field">
  <a href='<% $c->uri_for("/view/object", $referenced_object->table(), SmallRNA::DB::id_of_object($field_value)) %>'>
    <& field.mhtml, object => $referenced_object, field_label => $ref_display_key &>
  </a>
  </span>
%   } else {
%     if ($field_type eq 'key_field') {
  <span class="attribute_field">
    <span class="display_key">
      <a href='<% $c->uri_for("/view/object", $type, SmallRNA::DB::id_of_object($object)) %>'>
        <% $field_value %>
      </a>
    </span>
%     } else {
    <% $formatted_field_value %>
%     }
  </span>
%   }
% }
</span>

<%init>
use SmallRNA::DB;
use SmallRNA::Web::Util;

use Number::Format;

my $fmt = Number::Format->new();
my $type = $object->table();

my ($field_value, $field_type, $ref_display_key, $referenced_object) = ();

if ($col->{conf} =~ /[\$\-<>\';]/) {
  # Perl code
  $field_type = 'attribute';
  $field_value = eval $col->{conf};
  if ($@) {
    $field_value = $@;
    warn "$@\n";
  }
} else {
  # a field name
  ($field_value, $field_type, $ref_display_key) =
    SmallRNA::Web::Util::get_field_value($c, $object, $col->{conf});

  my $type = $object->table();

  if ($field_type eq 'foreign_key') {
    $referenced_object = $field_value;
  }

  if (!defined $field_value) {
    $field_value = '';
  }
}

my $formatted_field_value;

if (defined $field_value && defined $col->{format}) {
  if ($col->{format} eq 'integer') {
    $formatted_field_value = $fmt->format_number($field_value);
  } else {
    if ($col->{format} =~ /\%/) {
      $formatted_field_value = sprintf $col->{format}, $field_value;
    } else {
      die "unknown column format: ", $col->{format}, "\n";
    }
  }
} else {
  $formatted_field_value = $field_value;
}

</%init>
